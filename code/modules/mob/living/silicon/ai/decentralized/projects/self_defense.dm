/datum/ai_project/shock_defense
	name = "Shock Defense"
	description = "This research enables the option to shock people within 2 tiles of all of your data cores."
	research_cost = 3000
	ram_required = 0
	research_requirements = list(/datum/ai_project/induction_basic)
	can_be_run = FALSE

	category = AI_PROJECT_INDUCTION

	ability_path = /datum/action/innate/ai/shock_defense
	ability_recharge_cost = 2000

/datum/ai_project/shock_defense/finish()
	add_ability(/datum/action/innate/ai/shock_defense)

/datum/action/innate/ai/shock_defense
	name = "Shock Defense"
	desc = "Shocks anyone within 2 tiles of your data cores."
	button_icon_state = "emergency_lights"
	uses = 2
	delete_on_empty = FALSE

/datum/action/innate/ai/shock_defense/Activate()
	if(!isaicore(owner.loc))
		to_chat(owner, span_warning("You must be in your core to do this!"))
		return
	var/turf/ai_location_turf = get_turf(owner_AI)
	for(var/obj/machinery/ai/data_core/core in GLOB.data_cores["[ai_location_turf.z]"])
		tesla_zap(core, 2, (8 KILO JOULES), (ZAP_MOB_STUN|ZAP_MOB_DAMAGE))
		core.use_energy(core.idle_power_usage)
