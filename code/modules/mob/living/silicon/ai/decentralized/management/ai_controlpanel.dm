GLOBAL_VAR_INIT(ai_control_code, random_nukecode(6))

/obj/machinery/computer/ai_control_console
	name = "\improper AI control console"
	desc = "Used for accessing the central AI repository from which AIs can be downloaded or uploaded."
	req_access = list(ACCESS_RD)
	circuit = /obj/item/circuitboard/computer/ai_upload_download
	icon_keyboard = "tech_key"
	icon_screen = "ai-fixer"
	light_color = LIGHT_COLOR_PINK

	authenticated = FALSE

	var/one_time_password_used = FALSE //Did we use the one time password to log in? If so disallow logging out.

	var/obj/item/aicard/intellicard

	var/mob/living/silicon/ai/downloading
	var/mob/user_downloading
	var/download_progress = 0
	var/download_warning = FALSE

/obj/machinery/computer/ai_control_console/Initialize(mapload, obj/item/circuitboard/C)
	. = ..()
	if(!is_station_level(z))
		req_access = null
		req_one_access = list(ACCESS_AWAY_GENERAL)

/obj/machinery/computer/ai_control_console/item_interaction(mob/living/user, obj/item/tool, list/modifiers)
	if(istype(tool, /obj/item/aicard))
		if(intellicard)
			balloon_alert(user, "intellicard already in!")
			return ITEM_INTERACT_BLOCKING
		if(user.transferItemToLoc(tool, src))
			to_chat(user, span_notice("You insert [tool] into \the [src]."))
			intellicard = tool
			playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 25, FALSE)
			return ITEM_INTERACT_SUCCESS
		return ITEM_INTERACT_BLOCKING

	if(istype(tool, /obj/item/mmi))
		if(!authenticated)
			balloon_alert(user, "must be logged in!")
			return ITEM_INTERACT_BLOCKING
		var/obj/item/mmi/brain = tool
		if(!brain.brainmob?.mind)
			balloon_alert(user, "brain not active!")
			return ITEM_INTERACT_BLOCKING
		var/mob/living/silicon/ai/A = null

		var/datum/ai_laws/laws = new
		laws.set_laws_config()

		brain.try_unbrainwash()
		if(brain.overrides_ai_laws)
			A = new /mob/living/silicon/ai(loc, brain.laws, brain.brainmob)
		else
			A = new /mob/living/silicon/ai(loc, laws, brain.brainmob)
		A.relocate(TRUE)

		if(!istype(A.laws, /datum/ai_laws/ratvar))
			A.mind.remove_all_antag_datums()
			A.mind.wipe_memory()

		SSblackbox.record_feedback("amount", "ais_created", 1)
		qdel(tool)
		balloon_alert(user, "ai uploaded")
		playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 25, FALSE)
		return ITEM_INTERACT_SUCCESS

	if(istype(tool, /obj/item/surveillance_upgrade))
		if(!authenticated)
			balloon_alert(user, "must be logged in!")
			return ITEM_INTERACT_BLOCKING
		var/mob/living/silicon/ai/AI = tgui_input_list(user, "Select an AI", "Select an AI", GLOB.ai_list)
		if(!AI)
			return ITEM_INTERACT_BLOCKING
		playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 25, FALSE)
		var/obj/item/surveillance_upgrade/upgrade = tool
		return upgrade.interact_with_atom(AI, user)

	if(istype(tool, /obj/item/malf_upgrade))
		if(!authenticated)
			balloon_alert(user, "must be logged in!")
			return ITEM_INTERACT_BLOCKING
		var/mob/living/silicon/ai/AI = tgui_input_list(user, "Select an AI", "Select an AI", GLOB.ai_list)
		if(!AI)
			return ITEM_INTERACT_BLOCKING
		playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 25, FALSE)
		var/obj/item/malf_upgrade/upgrade = tool
		return upgrade.interact_with_atom(AI, user)

/obj/machinery/computer/ai_control_console/emag_act(mob/user, obj/item/card/emag/emag_card)
	. = ..()
	if(obj_flags & EMAGGED)
		return
	authenticated = TRUE
	obj_flags |= EMAGGED
	if(user)
		balloon_alert(user, "restrictions bypassed")

/obj/machinery/computer/ai_control_console/click_alt(mob/user)
	eject_intellicard(user)
	return CLICK_ACTION_SUCCESS

/obj/machinery/computer/ai_control_console/process(seconds_per_tick)
	if(machine_stat & (BROKEN|NOPOWER|EMPED))
		return

	if(downloading && download_progress >= 50 && !download_warning)
		var/turf/T = get_turf(src)
		to_chat(downloading, span_userdanger("Warning! Download is 50% completed! Download location: [get_area(src)] ([T.x], [T.y], [T.z])!"))
		download_warning = TRUE
	if(downloading && download_progress >= 100)
		finish_download()

	if(downloading)
		if(!downloading.can_download)
			stop_download()
			return
		download_progress += (AI_DOWNLOAD_PER_PROCESS * seconds_per_tick * downloading.downloadSpeedModifier)


/obj/machinery/computer/ai_control_console/ui_interact(mob/user, datum/tgui/ui)
	. = ..()
	ui = SStgui.try_update_ui(user, src, ui)
	if(!ui)
		ui = new(user, src, "AiControlPanel", name)
		ui.open()

/obj/machinery/computer/ai_control_console/ui_data(mob/living/carbon/human/user)
	var/list/data = list()
	data["authenticated"] = authenticated

	if(issilicon(user))
		var/mob/living/silicon/borg = user
		data["username"] = borg.name
		data["has_access"] = TRUE
	if(isAdminGhostAI(user))
		data["username"] = user.client.holder.admin_signature
		data["has_access"] = TRUE

	data["can_log_out"] = !one_time_password_used

	if(obj_flags & EMAGGED)
		data["username"] = "ERROR"
		data["has_access"] = TRUE
	else if(ishuman(user))
		data["has_access"] = allowed(user)
		var/username = user.get_authentification_name("Unknown")
		data["username"] = user.get_authentification_name("Unknown")
		if(username != "Unknown")
			var/datum/data/record/record
			for(var/RP in GLOB.manifest.general)
				var/datum/data/record/R = RP

				if(!istype(R))
					continue
				if(R.fields["name"] == username)
					record = R
					break
			if(record)
				if(istype(record.fields["photo_front"], /obj/item/photo))
					var/obj/item/photo/P1 = record.fields["photo_front"]
					var/icon/picture = icon(P1.picture.picture_image)
					picture.Crop(10, 32, 22, 22)
					var/md5 = md5(fcopy_rsc(picture))

					if(!SSassets.cache["photo_[md5]_cropped.png"])
						SSassets.transport.register_asset("photo_[md5]_cropped.png", picture)
					SSassets.transport.send_assets(user, list("photo_[md5]_cropped.png" = picture))

					data["user_image"] = SSassets.transport.get_asset_url("photo_[md5]_cropped.png")
	else
		data["has_access"] = allowed(user)
		data["username"] = user.name

	if(!authenticated)
		return data

	data["intellicard"] = intellicard
	if(intellicard && intellicard.AI)
		data["intellicard_ai"] = intellicard.AI.real_name
		data["intellicard_ai_health"] = intellicard.AI.health
		data["can_upload"] = intellicard.AI.find_valid_ai_core()
	else
		data["intellicard_ai"] = null
		data["intellicard_ai_health"] = 0
		data["can_upload"] = FALSE

	if(downloading)
		data["downloading"] = downloading.real_name
		data["download_progress"] = download_progress
		data["downloading_ref"] = REF(downloading)
	else
		data["downloading"] = null
		data["download_progress"] = 0

	data["ais"] = list()
	data["current_ai_ref"] = null
	if(isAI(user))
		data["current_ai_ref"] = REF(user)

	var/turf/computer_turf = get_turf(src)
	for(var/obj/machinery/ai/data_core as anything in GLOB.data_cores["[computer_turf.z]"])
		for(var/mob/living/silicon/ai/A in data_core.contents)
			data["ais"] += list(list(
				"name" = A.name,
				"ref" = REF(A),
				"can_download" = A.can_download,
				"health" = A.health,
				"active" = !!(A.mind),
				"in_core" = istype(A.loc, /obj/machinery/ai/data_core),
			))

	return data

/obj/machinery/computer/ai_control_console/proc/eject_intellicard(mob/living/user)
	if(issilicon(user))
		to_chat(user, span_warning("You're unable to remotely eject [intellicard]!"))
		return FALSE
	stop_download()
	if(!user.put_in_hands(intellicard))
		intellicard.forceMove(drop_location())
	intellicard = null
	playsound(src, 'sound/machines/terminal_insert_disc.ogg', 30, FALSE)
	return TRUE

/obj/machinery/computer/ai_control_console/proc/finish_download()
	if(!(downloading.loc in GLOB.data_cores["[z]"]))
		return
	if(intellicard)
		playsound(src, 'sound/machines/terminal_prompt_confirm.ogg', 25, FALSE)
		downloading.transfer_ai(AI_TRANS_TO_CARD, user_downloading, null, intellicard)
		intellicard.update_appearance()
	stop_download(TRUE)

/obj/machinery/computer/ai_control_console/proc/stop_download(silent = FALSE)
	if(!downloading)
		return
	if(!silent)
		to_chat(downloading, span_userdanger("Download stopped."))
	downloading = null
	user_downloading = null
	download_progress = 0
	download_warning = FALSE

/obj/machinery/computer/ai_control_console/proc/upload_ai(silent = FALSE)
	playsound(src, 'sound/machines/terminal_processing.ogg', 25, FALSE)
	to_chat(intellicard.AI, span_notice("You are being uploaded. Please stand by..."))
	intellicard.AI.radio_enabled = TRUE
	intellicard.AI.control_disabled = FALSE
	intellicard.AI.relocate(TRUE)
	intellicard.AI = null
	intellicard.update_appearance()

/obj/machinery/computer/ai_control_console/ui_act(action, list/params, datum/tgui/ui, datum/ui_state/state)
	. = ..()
	if(.)
		return

	var/mob/user = ui.user

	if(!authenticated)
		if(action == "log_in")
			if(allowed(user) || (obj_flags & EMAGGED))
				playsound(src, SFX_TERMINAL_TYPE, 50, FALSE)
				authenticated = TRUE
				return TRUE
			return .
		if(action == "log_in_control_code")
			var/code = text2num(params["control_code"])

			var/length_of_number = round(log(10, code) + 1)
			if(length_of_number < 6)
				to_chat(user, span_warning("Incorrect code. Too short."))
				return

			if(length_of_number > 6)
				to_chat(user, span_warning("Incorrect code. Too long."))
				return

			if(!GLOB.ai_control_code)
				return

			if(code == text2num(GLOB.ai_control_code))
				authenticated = TRUE
				one_time_password_used = TRUE
				var/msg = "<h4>Warning!</h4><br>We have detected usage of the AI Control Code for unlocking a console at coordinates ([src.x], [src.y], [src.z]) by [user.name]. Please verify that this is correct. Be aware we have cancelled the current control code.<br>\
				If needed a new code can be printed at a communications console."
				priority_announce(msg, sender_override = "Central Cyber Security Update", has_important_message = TRUE, encode_text = FALSE)
				GLOB.ai_control_code = null
			else
				to_chat(user, span_warning("Incorrect code. Make sure you have the latest one."))
		return

	switch(action)
		if("log_out")
			if(one_time_password_used)
				return
			playsound(src, SFX_TERMINAL_TYPE, 50, FALSE)
			authenticated = FALSE
			. = TRUE
		if("upload_intellicard")
			if(!intellicard || downloading)
				return
			if(!intellicard.AI)
				return
			upload_ai()

		if("eject_intellicard")
			return eject_intellicard(user)

		if("stop_download")
			if(isAI(user))
				to_chat(user, span_warning("You need physical access to stop the download!"))
				return
			stop_download()
			playsound(src, 'sound/machines/terminal_prompt_deny.ogg', 25, FALSE)

		if("start_download")
			if(!intellicard || downloading)
				return
			var/mob/living/silicon/ai/target = locate(params["download_target"])
			if(!target || !istype(target))
				return
			if(!isvalidAIloc(target.loc))
				return
			if(!target.can_download)
				return
			if(!(target.loc in GLOB.data_cores["[z]"]))
				to_chat(user, span_warning("No connection. Try again later."))
				return
			playsound(src, 'sound/machines/terminal_prompt_confirm.ogg', 25, FALSE)
			downloading = target
			to_chat(downloading, span_userdanger("Warning! Someone is attempting to download you from [get_area(src)]! (<a href='byond://?src=[REF(downloading)];instant_download=1;console=[REF(src)]'>Click here to finish download instantly</a>)"))
			user_downloading = user
			download_progress = 0
			. = TRUE
		if("skip_download")
			if(!downloading)
				return
			if(user == downloading)
				finish_download()

/obj/item/paper/ai_control_code
	name = "paper - 'AI control code'"

/obj/item/paper/ai_control_code/Initialize(mapload)
	. = ..()
	add_raw_text("<center><h2>Daily AI Control Key Reset</h2></center><br>The new authentication key is '[GLOB.ai_control_code]'.<br>\
		Please keep this a secret and away from the clown.<br>This code may be invalidated if a new one is requested.")
	update_appearance()
