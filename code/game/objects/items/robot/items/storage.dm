/obj/item/borg/apparatus
	name = "unknown storage apparatus"
	desc = "This device seems nonfunctional."
	icon = 'icons/mob/silicon/robot_items.dmi'
	icon_state = "hugmodule"
	/// The item stored inside of this apparatus
	var/obj/item/stored
	/// Whitelist of types (and its subtypes) that are allowed in this apparatus.
	var/list/whitelist_storables = list()
	/// Blacklist of types (and its subtypes) that are not allowed in this apparatus.
	var/list/blacklisted_storables = list()
	/// Can this interact with various electronics?
	var/allow_electronics_interaction = FALSE

/obj/item/borg/apparatus/Initialize(mapload)
	. = ..()
	if(iscyborg(loc))
		RegisterSignal(loc, COMSIG_BORG_SAFE_DECONSTRUCT, PROC_REF(safe_deconstruct))

/obj/item/borg/apparatus/Destroy()
	if(!isnull(stored))
		QDEL_NULL(stored)
	return ..()

/obj/item/borg/apparatus/Exited(atom/movable/gone, direction)
	if(gone == stored) // Sanity check.
		UnregisterSignal(stored, COMSIG_ATOM_UPDATED_ICON)
		stored = null
	update_appearance()
	return ..()

/obj/item/borg/apparatus/examine(mob/user)
	. = ..()
	if(stored)
		. += span_notice("[EXAMINE_HINT("Alt-click")] to drop your stored item.")

// Attack_self will pass for the stored item.
/obj/item/borg/apparatus/attack_self(mob/living/silicon/robot/user)
	if(!stored || !issilicon(user))
		return ..()
	stored.attack_self(user)

/obj/item/borg/apparatus/attack_self_secondary(mob/living/silicon/robot/user)
	if(!stored || !issilicon(user))
		return ..()
	stored.attack_self_secondary(user)

// Alt-click drops the stored item.
/obj/item/borg/apparatus/click_alt(mob/living/silicon/robot/user)
	if(!stored || !issilicon(user))
		return CLICK_ACTION_BLOCKING
	stored.forceMove(user.drop_location())
	return CLICK_ACTION_SUCCESS

/obj/item/borg/apparatus/get_proxy_attacker_for(atom/target, mob/user)
	if(stored)
		return stored
	return ..()

/obj/item/borg/apparatus/interact_with_atom(atom/interacting_with, mob/living/user, list/modifiers)
	if(!allow_electronics_interaction)
		return NONE
	// Mimicking a hand interaction with certain atoms to pull out batteries.
	// Closed machines:
	if(istype(interacting_with, /obj/machinery/cell_charger))
		var/obj/machinery/cell_charger/charger_machinery = interacting_with
		if(stored != null || charger_machinery.panel_open)
			return ITEM_INTERACT_BLOCKING
		charger_machinery.attack_hand(user)
		return ITEM_INTERACT_SUCCESS
	if(istype(interacting_with, /obj/machinery/cell_charger_multi))
		var/obj/machinery/cell_charger_multi/charger_multi_machinery = interacting_with
		if(stored != null || charger_multi_machinery.panel_open)
			return ITEM_INTERACT_BLOCKING
		charger_multi_machinery.attack_hand(user)
		return ITEM_INTERACT_SUCCESS
	// Opened machines:
	if(istype(interacting_with, /obj/machinery/button))
		var/obj/machinery/button/button_machinery = interacting_with
		if(stored != null || !button_machinery.panel_open)
			return ITEM_INTERACT_BLOCKING
		button_machinery.attack_hand(user)
		return ITEM_INTERACT_SUCCESS
	if(istype(interacting_with, /obj/machinery/space_heater))
		var/obj/machinery/space_heater/heater_machinery = interacting_with
		if(stored != null || !heater_machinery.panel_open)
			return ITEM_INTERACT_BLOCKING
		heater_machinery.attack_hand(user)
		return ITEM_INTERACT_SUCCESS
	if(isapc(interacting_with))
		var/obj/machinery/power/apc/apc_machinery = interacting_with
		if(stored != null || !apc_machinery.opened || !apc_machinery.cell)
			return ITEM_INTERACT_BLOCKING
		var/obj/item/stock_parts/power_store/cell/removed_cell = apc_machinery.cell
		user.visible_message(span_notice("[user] removes [removed_cell] from [src]!"))
		balloon_alert(user, "cell removed")
		removed_cell.update_appearance()
		user.put_in_hands(removed_cell)
		apc_machinery.cell = null
		apc_machinery.charging = APC_NOT_CHARGING
		apc_machinery.update_appearance()
		return ITEM_INTERACT_SUCCESS
	// Cyborgs:
	if(iscyborg(interacting_with) && interacting_with != user)
		var/mob/living/silicon/robot/touched_cyborg = interacting_with
		if(stored != null || !touched_cyborg.cell || !touched_cyborg.opened || touched_cyborg.wiresexposed)
			return ITEM_INTERACT_BLOCKING
		var/obj/item/stock_parts/power_store/cell/removed_cell = touched_cyborg.cell
		to_chat(user, span_notice("You remove \the [removed_cell]."))
		removed_cell.update_appearance()
		removed_cell.add_fingerprint(user)
		user.put_in_hands(removed_cell)
		touched_cyborg.update_icons()
		touched_cyborg.diag_hud_set_borgcell()
		return ITEM_INTERACT_SUCCESS
	return NONE

/obj/item/borg/apparatus/pre_attack(atom/target, mob/living/user, list/modifiers, list/attack_modifiers)
	if(put_in_apparatus(target, user))
		return TRUE
	return ..()

// Eject containers from machines and inserts it into the apparatus.
/obj/item/borg/apparatus/pre_attack_secondary(atom/target, mob/living/user, list/modifiers, list/attack_modifiers)
	for(var/atom/atom_content in target.contents)
		if(!is_acceptable_storable(atom_content))
			continue
		return target.attack_hand_secondary(user, modifiers)
	return ..()

/obj/item/borg/apparatus/attackby(obj/item/attacking_item, mob/user, list/modifiers, list/attack_modifiers)
	if(stored)
		attacking_item.melee_attack_chain(user, stored, modifiers, attack_modifiers)
		return TRUE
	return ..()

/// Checks if the item is allowed to be inside of the apparatus.
/obj/item/borg/apparatus/proc/is_acceptable_storable(atom/target)
	if(is_type_in_list(target, blacklisted_storables))
		return FALSE
	return is_type_in_list(target, whitelist_storables)

/// Attempts to put the item into the apparatus.
/obj/item/borg/apparatus/proc/put_in_apparatus(obj/item/storing_item, mob/user)
	if(stored)
		return FALSE
	if(!istype(storing_item))
		return FALSE
	if(HAS_TRAIT(storing_item, TRAIT_NODROP))
		return FALSE
	if(istype(storing_item.loc, /obj/item/robot_model)) // Taking stuff from our inventory.
		return FALSE
	if(iscyborg(storing_item.loc) && user == storing_item.loc) // Taking stuff from our active module slots.
		return FALSE
	if(!is_acceptable_storable(storing_item))
		return FALSE
	storing_item.forceMove(src)
	stored = storing_item
	RegisterSignal(stored, COMSIG_ATOM_UPDATED_ICON, PROC_REF(on_stored_updated_icon))
	update_appearance()
	return TRUE

/// If we're safely deconstructed, we put the item neatly onto the ground, rather than deleting it.
/obj/item/borg/apparatus/proc/safe_deconstruct()
	SIGNAL_HANDLER
	if(!stored)
		return
	stored.forceMove(get_turf(src))
	stored = null

/**
 * Updates the appearance of the apparatus when the stored object's icon gets updated.
 *
 * Returns NONE as we have not done anything to the stored object itself,
 * which is where this signal that this handler intercepts is sent from.
 */
/obj/item/borg/apparatus/proc/on_stored_updated_icon(datum/source, updates)
	SIGNAL_HANDLER
	update_appearance()
	return NONE

/// A right-click verb, for those not using hotkey mode.
/obj/item/borg/apparatus/verb/verb_drop_stored_item()
	set category = "Object"
	set name = "Drop"

	if(usr != loc || !stored)
		return
	stored.forceMove(get_turf(usr))

/obj/item/borg/apparatus/beaker
	name = "beaker storage apparatus"
	desc = "A special apparatus for carrying beakers without spilling the contents."
	icon_state = "borg_beaker_apparatus"
	whitelist_storables = list(
		/obj/item/reagent_containers/cup/beaker,
		/obj/item/reagent_containers/cup/bottle,
		/obj/item/reagent_containers/cup/tube,
		/obj/item/weapon/virusdish
	)

/obj/item/borg/apparatus/beaker/Initialize(mapload)
	add_glass()
	RegisterSignal(stored, COMSIG_ATOM_UPDATED_ICON, PROC_REF(on_stored_updated_icon))
	update_appearance()
	return ..()

/obj/item/borg/apparatus/beaker/proc/add_glass()
	stored = new /obj/item/reagent_containers/cup/beaker/large(src)

/obj/item/borg/apparatus/beaker/Destroy()
	if(stored)
		var/obj/item/reagent_containers/reagent_container = stored
		reagent_container.SplashReagents(get_turf(src))
	QDEL_NULL(stored)
	return ..()

/obj/item/borg/apparatus/beaker/update_overlays()
	. = ..()
	var/mutable_appearance/arm = mutable_appearance(icon = icon, icon_state = "borg_beaker_apparatus_arm")
	if(stored)
		stored.pixel_x = 0
		stored.pixel_y = 0
		var/mutable_appearance/stored_copy = new /mutable_appearance(stored)
		if(istype(stored, /obj/item/reagent_containers/cup/beaker))
			arm.pixel_y = arm.pixel_y - 3
		stored_copy.layer = FLOAT_LAYER
		stored_copy.plane = FLOAT_PLANE
		. += stored_copy
	else
		arm.pixel_y = arm.pixel_y - 5
	. += arm

/// Secondary attack spills the content of the beaker.
/obj/item/borg/apparatus/beaker/pre_attack_secondary(atom/target, mob/living/silicon/robot/user)
	var/obj/item/reagent_containers/stored_beaker = stored
	if(!stored_beaker)
		return ..()
	stored_beaker.SplashReagents(drop_location(user))
	loc.visible_message(span_notice("[user] spills the contents of [stored_beaker] all over the ground."))
	return ..()

/obj/item/borg/apparatus/beaker/extra
	name = "secondary beaker storage apparatus"
	desc = "A supplementary beaker storage apparatus."

/obj/item/borg/apparatus/beaker/service
	name = "beverage storage apparatus"
	desc = "A special apparatus for carrying drinks without spilling the contents. Will resynthesize any drinks you pour out!"
	icon_state = "borg_beaker_apparatus"
	whitelist_storables = list(
		/obj/item/reagent_containers/cup/beaker,
		/obj/item/reagent_containers/cup/bottle,
		/obj/item/reagent_containers/cup/glass,
		/obj/item/reagent_containers/condiment,
		/obj/item/reagent_containers/cup/coffeepot
	)

/obj/item/borg/apparatus/beaker/service/add_glass()
	stored = new /obj/item/reagent_containers/cup/glass/drinkingglass(src)
	handle_reflling(stored, loc.loc, force = TRUE)

/obj/item/borg/apparatus/beaker/service/proc/handle_reflling(obj/item/reagent_containers/cup/glass, mob/living/silicon/robot/bro, force = FALSE)
	if (isnull(bro))
		bro = loc
	if (!iscyborg(bro))
		return

	if (!stored || force)
		glass.AddComponent(/datum/component/reagent_refiller, power_draw_callback = CALLBACK(bro, TYPE_PROC_REF(/mob/living/silicon/robot, draw_power)))

/obj/item/borg/apparatus/beaker/service/Entered(atom/movable/arrived, atom/old_loc, list/atom/old_locs)
	if (!istype(arrived, /obj/item/reagent_containers/cup/glass))
		return
	handle_reflling(arrived)
	return ..()

/// allows medical cyborgs to manipulate organs without hands
/obj/item/borg/apparatus/organ_storage
	name = "organ storage bag"
	desc = "A container for holding body parts."
	icon = 'icons/obj/storage/storage.dmi'
	icon_state = "evidenceobj"
	item_flags = SURGICAL_TOOL
	whitelist_storables = list(
		/obj/item/organ,
		/obj/item/bodypart
	)

/obj/item/borg/apparatus/organ_storage/update_overlays()
	. = ..()
	icon_state = null // hides the original icon (otherwise it's drawn underneath)
	var/mutable_appearance/bag
	if(stored)
		var/mutable_appearance/stored_organ = new /mutable_appearance(stored)
		stored_organ.layer = FLOAT_LAYER
		stored_organ.plane = FLOAT_PLANE
		stored_organ.pixel_x = 0
		stored_organ.pixel_y = 0
		. += stored_organ
		bag = mutable_appearance(icon, icon_state = "evidence") // full bag
	else
		bag = mutable_appearance(icon, icon_state = "evidenceobj") // empty bag
	. += bag

/obj/item/borg/apparatus/organ_storage/click_alt(mob/living/silicon/robot/user)
	if(!stored)
		to_chat(user, span_notice("[src] is empty."))
		return CLICK_ACTION_BLOCKING

	var/obj/item/organ = stored
	user.visible_message(span_notice("[user] dumps [organ] from [src]."), span_notice("You dump [organ] from [src]."))
	cut_overlays()
	organ.forceMove(get_turf(src))
	return CLICK_ACTION_SUCCESS

/obj/item/borg/apparatus/organ_storage/monster
	name = "core storage bag"
	desc = "A container for holding and application of various monster organs."
	whitelist_storables = list(/obj/item/organ/internal/monster_core)

/obj/item/borg/apparatus/organ_storage/limb
	name = "limb storage bag"
	desc = "A container for holding limbs."
	whitelist_storables = list(/obj/item/bodypart)

///Apparatus to allow Engineering/Sabo borgs to manipulate any material sheets.
/obj/item/borg/apparatus/sheet_manipulator
	name = "material manipulation apparatus"
	desc = "An apparatus for carrying, deploying, and manipulating sheets of material. The device can also carry custom floor tiles."
	icon_state = "borg_stack_apparatus"
	whitelist_storables = list(
		/obj/item/stack/sheet,
		/obj/item/stack/rods,
		/obj/item/stack/ore/bluespace_crystal,
		/obj/item/stack/tile,
		/obj/item/flatpacked_machine
	)

/obj/item/borg/apparatus/sheet_manipulator/Initialize(mapload)
	update_appearance()
	return ..()

/obj/item/borg/apparatus/sheet_manipulator/update_overlays()
	. = ..()
	var/mutable_appearance/arm = mutable_appearance(icon, "borg_stack_apparatus_arm1")
	if(stored)
		stored.pixel_x = 0
		stored.pixel_y = 0
		arm.icon_state = "borg_stack_apparatus_arm2"
		var/mutable_appearance/stored_copy = new /mutable_appearance(stored)
		var/underscore = findtext(stored_copy.icon_state, "_")
		if(underscore)
			stored_copy.icon_state = initial(stored.icon_state) //how we use the icon_state of single sheets, even with full stacks
		stored_copy.layer = FLOAT_LAYER
		stored_copy.plane = FLOAT_PLANE
		. += stored_copy
	. += arm

/obj/item/borg/apparatus/sheet_manipulator/extra
	name = "secondary material manipulation apparatus"
	desc = "A supplementary apparatus for carrying, deploying, and manipulating sheets of material. The device can also carry custom floor tiles."

///Apparatus allowing Engineer/Sabo borgs to manipulate Machine and Computer circuit boards
/obj/item/borg/apparatus/circuit
	name = "electronics manipulation apparatus"
	desc = "A special apparatus for carrying and manipulating electronics like circuit boards, cells, stock parts, signalers and etc."
	icon_state = "borg_hardware_apparatus"
	whitelist_storables = list(
		/obj/item/circuitboard,
		/obj/item/electronics,
		/obj/item/stock_parts,
		/obj/item/assembly,
		/obj/item/flatpacked_machine
	)
	allow_electronics_interaction = TRUE

/obj/item/borg/apparatus/circuit/Initialize(mapload)
	update_appearance()
	return ..()

/obj/item/borg/apparatus/circuit/update_overlays()
	. = ..()
	var/mutable_appearance/arm = mutable_appearance(icon, "borg_hardware_apparatus_arm1")
	if(stored)
		stored.pixel_x = -3
		stored.pixel_y = 0
		if(!istype(stored, /obj/item/circuitboard))
			arm.icon_state = "borg_hardware_apparatus_arm2"
		var/mutable_appearance/stored_copy = new /mutable_appearance(stored)
		stored_copy.layer = FLOAT_LAYER
		stored_copy.plane = FLOAT_PLANE
		. += stored_copy
	. += arm

/obj/item/borg/apparatus/circuit/pre_attack(atom/atom, mob/living/user, params)
	if(istype(atom, /obj/item/ai_module) && !stored) //If an admin wants a borg to upload laws, who am I to stop them? Otherwise, we can hint that it fails
		to_chat(user, span_warning("This circuit board doesn't seem to have standard robot apparatus pin holes. You're unable to pick it up."))
	return ..()

/obj/item/borg/apparatus/circuit/science
	name = "science manipulation apparatus"
	desc = "A special apparatus for carrying various stock parts, disks, assemblies, and even artifacts!"
	whitelist_storables = list(
		/obj/item/stock_parts,
		/obj/item/assembly,
		/obj/item/disk,
		/obj/item/artifact_item,
		/obj/item/artifact_item_tiny,
		/obj/item/gun/magic/artifact,
		/obj/item/melee/artifact,
		/obj/item/artifact_summon_wand,
		/obj/item/slime_mutation_syringe,
		/obj/item/borg_restart_board
	)
	blacklisted_storables = list(
		/obj/item/disk/nuclear
	)

//apparatus to allow borgs to cook
/obj/item/borg/apparatus/cooking
	name = "service storage apparatus"
	desc = "A special apparatus for carrying food, bowls, plates, oven trays, soup pots and paper."
	icon_state = "borg_service_apparatus"
	whitelist_storables = list(
		/obj/item/food,
		/obj/item/paper,
		/obj/item/plate,
		/obj/item/reagent_containers/cup/bowl,
		/obj/item/reagent_containers/cup/soup_pot,
		/obj/item/seeds,
		/obj/item/stack/biocube,
		/obj/item/folder,
		/obj/item/clipboard
	)

/obj/item/borg/apparatus/cooking/Initialize(mapload)
	RegisterSignal(stored, COMSIG_ATOM_UPDATED_ICON, PROC_REF(on_stored_updated_icon))
	update_appearance()
	return ..()

/obj/item/borg/apparatus/cooking/update_overlays()
	. = ..()
	var/mutable_appearance/arm = mutable_appearance(icon, "borg_hardware_apparatus_arm1")
	if(stored)
		stored.pixel_x = -3
		stored.pixel_y = 0
		if((!istype(stored, /obj/item/plate/oven_tray)) || (!istype(stored, /obj/item/food)))
			arm.icon_state = "borg_hardware_apparatus_arm2"
		var/mutable_appearance/stored_copy = new /mutable_appearance(stored)
		stored_copy.layer = FLOAT_LAYER
		stored_copy.plane = FLOAT_PLANE
		. += stored_copy
	. += arm
