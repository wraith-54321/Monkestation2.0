/obj/item/storage/lockbox
	name = "lockbox"
	desc = "A locked box."
	icon = 'icons/obj/storage/case.dmi'
	icon_state = "lockbox+l"
	inhand_icon_state = "lockbox"
	lefthand_file = 'icons/mob/inhands/equipment/briefcase_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/briefcase_righthand.dmi'
	w_class = WEIGHT_CLASS_BULKY
	req_access = list(ACCESS_ARMORY)
	var/broken = FALSE
	var/open = FALSE
	var/icon_locked = "lockbox+l"
	var/icon_closed = "lockbox"
	var/icon_open = "lockbox"
	var/icon_broken = "lockbox+b"
	///If the lockbox starts locked
	var/start_locked = TRUE

///screentips for lockboxes
/obj/item/storage/lockbox/add_context(atom/source, list/context, obj/item/held_item, mob/user)
	if(!held_item)
		return NONE
	if(src.broken)
		return NONE
	if(!held_item.GetID())
		return NONE
	context[SCREENTIP_CONTEXT_LMB] = atom_storage.locked ? "Unlock with ID" : "Lock with ID"
	return CONTEXTUAL_SCREENTIP_SET

/obj/item/storage/lockbox/Initialize(mapload)
	. = ..()
	atom_storage.max_specific_storage = WEIGHT_CLASS_NORMAL
	atom_storage.max_total_storage = 14
	atom_storage.max_slots = 4
	if(start_locked)
		atom_storage.set_locked(STORAGE_FULLY_LOCKED)

	register_context()
	update_icon_state()

///screentips for lockboxes
/obj/item/storage/lockbox/add_context(atom/source, list/context, obj/item/held_item, mob/user)
	if(!held_item)
		return NONE
	if(broken)
		return NONE
	if(!held_item.GetID())
		return NONE
	context[SCREENTIP_CONTEXT_LMB] = atom_storage.locked ? "Unlock with ID" : "Lock with ID"
	return CONTEXTUAL_SCREENTIP_SET

/obj/item/storage/lockbox/tool_act(mob/living/user, obj/item/tool, list/modifiers)
	var/obj/item/card/card = tool.GetID()
	if(isnull(card))
		return ..()

	if(can_unlock(user, card))
		toggle_locked(user)
		return ITEM_INTERACT_SUCCESS

	return ITEM_INTERACT_BLOCKING

/obj/item/storage/lockbox/proc/can_unlock(mob/living/user, obj/item/card/id/id_card, silent = FALSE)
	if (broken) // emagged
		balloon_alert(user, "broken!")
		return FALSE
	if(check_access(id_card))
		return TRUE
	if(!silent)
		balloon_alert(user, "access denied!")
	return FALSE

/obj/item/storage/lockbox/proc/toggle_locked(mob/living/user)
	atom_storage.set_locked(atom_storage.locked ? STORAGE_NOT_LOCKED : STORAGE_FULLY_LOCKED)
	balloon_alert(user, atom_storage.locked ? "locked" : "unlocked")

/obj/item/storage/lockbox/update_icon_state()
	. = ..()
	if(broken)
		icon_state = icon_broken
	else if(atom_storage?.locked)
		icon_state = icon_locked
	else if(open)
		icon_state = icon_open
	else
		icon_state = icon_closed

/obj/item/storage/lockbox/emag_act(mob/user, obj/item/card/emag/emag_card)
	if(!broken)
		open = TRUE
		broken = TRUE
		atom_storage.set_locked(STORAGE_NOT_LOCKED)
		balloon_alert(user, "lock destroyed")
		if (emag_card && user)
			user.visible_message(span_warning("[user] swipes [emag_card] over [src], breaking it!"))
		return TRUE
	return FALSE

/obj/item/storage/lockbox/examine(mob/user)
	. = ..()
	if(broken)
		. += span_notice("It appears to be broken.")

/obj/item/storage/lockbox/Entered(atom/movable/arrived, atom/old_loc, list/atom/old_locs)
	. = ..()
	open = TRUE
	update_appearance()

/obj/item/storage/lockbox/Exited(atom/movable/gone, direction)
	. = ..()
	open = TRUE
	update_appearance()

/obj/item/storage/lockbox/loyalty
	name = "lockbox of mindshield implants"
	req_access = list(ACCESS_SECURITY)

/obj/item/storage/lockbox/loyalty/PopulateContents()
	for(var/i in 1 to 3)
		new /obj/item/implantcase/mindshield(src)
	new /obj/item/implanter/mindshield(src)

/obj/item/storage/lockbox/clusterbang
	name = "lockbox of clusterbangs"
	desc = "You have a bad feeling about opening this."
	req_access = list(ACCESS_SECURITY)

/obj/item/storage/lockbox/clusterbang/PopulateContents()
	new /obj/item/grenade/clusterbuster(src)

/obj/item/storage/lockbox/medal
	name = "medal box"
	desc = "A locked box used to store medals of honor."
	icon_state = "medalbox+l"
	inhand_icon_state = "syringe_kit"
	lefthand_file = 'icons/mob/inhands/equipment/medical_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/medical_righthand.dmi'
	w_class = WEIGHT_CLASS_NORMAL
	req_access = list(ACCESS_CAPTAIN)

/obj/item/storage/lockbox/medal/Initialize(mapload)
	. = ..()
	atom_storage.max_specific_storage = WEIGHT_CLASS_SMALL
	atom_storage.max_slots = 10
	atom_storage.max_total_storage = 20
	atom_storage.set_holdable(list(/obj/item/clothing/accessory/medal))

/obj/item/storage/lockbox/medal/examine(mob/user)
	. = ..()
	. += span_notice("Use in hand to [open ? "close it":"open it in order to access contents"].")

/obj/item/storage/lockbox/medal/attack_self(mob/user, modifiers)
	// . = ..()
	if (atom_storage?.locked < STORAGE_FULLY_LOCKED) // not fully locked
		if (open)
			open = FALSE
			atom_storage.set_locked(STORAGE_SOFT_LOCKED)
		else
			open = TRUE
			atom_storage.set_locked(STORAGE_NOT_LOCKED)
	return

/obj/item/storage/lockbox/medal/tool_act(mob/living/user, obj/item/tool, list/modifiers)
	var/obj/item/card/card = tool.GetID()
	if(isnull(card))
		return ..()

	if(can_unlock(user, card))
		if (!open) // Addition: can only relock if lid closed
			if (atom_storage?.locked > STORAGE_SOFT_LOCKED)
				// lid is closed, so still soft locked
				atom_storage.set_locked(STORAGE_SOFT_LOCKED)
			else
				atom_storage.set_locked(STORAGE_FULLY_LOCKED)
			return ITEM_INTERACT_SUCCESS
		balloon_alert(user, "close lid!")
	return ITEM_INTERACT_BLOCKING

/obj/item/storage/lockbox/medal/PopulateContents()
	new /obj/item/clothing/accessory/medal/gold/captain(src)
	new /obj/item/clothing/accessory/medal/silver/valor(src)
	new /obj/item/clothing/accessory/medal/silver/valor(src)
	new /obj/item/clothing/accessory/medal/silver/security(src)
	new /obj/item/clothing/accessory/medal/bronze_heart(src)
	new /obj/item/clothing/accessory/medal/plasma/nobel_science(src)
	new /obj/item/clothing/accessory/medal/plasma/nobel_science(src)
	for(var/i in 1 to 3)
		new /obj/item/clothing/accessory/medal/conduct(src)

/obj/item/storage/lockbox/medal/update_icon_state()
	. = ..()
	if(atom_storage?.locked > STORAGE_SOFT_LOCKED)
		icon_state = "medalbox+l"
		return

	icon_state = "medalbox"
	if(open)
		icon_state += "open"
	if(broken)
		icon_state += "+b"
	return

/obj/item/storage/lockbox/medal/update_overlays()
	. = ..()
	if(!contents || !open)
		return
	if(atom_storage?.locked)
		return
	for(var/i in 1 to contents.len)
		var/obj/item/clothing/accessory/medal/M = contents[i]
		var/mutable_appearance/medalicon = mutable_appearance(initial(icon), M.medaltype)
		if(i > 1 && i <= 5)
			medalicon.pixel_x += ((i-1)*3)
		else if(i > 5)
			medalicon.pixel_y -= 7
			medalicon.pixel_x -= 2
			medalicon.pixel_x += ((i-6)*3)
		. += medalicon

/obj/item/storage/lockbox/medal/hop
	name = "Head of Personnel medal box"
	desc = "A locked box used to store medals to be given to those exhibiting excellence in management."
	req_access = list(ACCESS_HOP)

/obj/item/storage/lockbox/medal/hop/PopulateContents()
	for(var/i in 1 to 3)
		new /obj/item/clothing/accessory/medal/silver/bureaucracy(src)
	new /obj/item/clothing/accessory/medal/gold/ordom(src)

/obj/item/storage/lockbox/medal/sec
	name = "security medal box"
	desc = "A locked box used to store medals to be given to members of the security department."
	req_access = list(ACCESS_HOS)

/obj/item/storage/lockbox/medal/med
	name = "medical medal box"
	desc = "A locked box used to store medals to be given to members of the medical department."
	req_access = list(ACCESS_CMO)

/obj/item/storage/lockbox/medal/med/PopulateContents()
	new /obj/item/clothing/accessory/medal/med_medal(src)
	new /obj/item/clothing/accessory/medal/med_medal2(src)
	for(var/i in 1 to 3)
		new /obj/item/clothing/accessory/medal/silver/emergency_services/medical(src)

/obj/item/storage/lockbox/medal/sec/PopulateContents()
	for(var/i in 1 to 3)
		new /obj/item/clothing/accessory/medal/silver/security(src)

/obj/item/storage/lockbox/medal/cargo
	name = "cargo award box"
	desc = "A locked box used to store awards to be given to members of the cargo department."
	req_access = list(ACCESS_QM)

/obj/item/storage/lockbox/medal/cargo/PopulateContents()
		new /obj/item/clothing/accessory/medal/ribbon/cargo(src)

/obj/item/storage/lockbox/medal/service
	name = "service award box"
	desc = "A locked box used to store awards to be given to members of the service department."
	req_access = list(ACCESS_HOP)

/obj/item/storage/lockbox/medal/service/PopulateContents()
		new /obj/item/clothing/accessory/medal/silver/excellence(src)

/obj/item/storage/lockbox/medal/sci
	name = "science medal box"
	desc = "A locked box used to store medals to be given to members of the science department."
	req_access = list(ACCESS_RD)

/obj/item/storage/lockbox/medal/sci/PopulateContents()
	for(var/i in 1 to 3)
		new /obj/item/clothing/accessory/medal/plasma/nobel_science(src)

/obj/item/storage/lockbox/medal/engineering
	name = "engineering medal box"
	desc = "A locked box used to store awards to be given to members of the engineering department."
	req_access = list(ACCESS_CE)

/obj/item/storage/lockbox/medal/engineering/PopulateContents()
	for(var/i in 1 to 3)
		new /obj/item/clothing/accessory/medal/silver/emergency_services/engineering(src)
	new /obj/item/clothing/accessory/medal/silver/elder_atmosian(src)

/obj/item/storage/lockbox/order
	name = "order lockbox"
	desc = "A box used to secure small cargo orders from being looted by those who didn't order it. Yeah, cargo tech, that means you."
	icon_state = "secure"
	icon_broken = "secure+b"
	inhand_icon_state = "sec-case"
	lefthand_file = 'icons/mob/inhands/equipment/briefcase_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/briefcase_righthand.dmi'
	w_class = WEIGHT_CLASS_HUGE
	var/datum/bank_account/buyer_account

/obj/item/storage/lockbox/order/Initialize(mapload, datum/bank_account/_buyer_account)
	. = ..()
	buyer_account = _buyer_account
	ADD_TRAIT(src, TRAIT_NO_MISSING_ITEM_ERROR, TRAIT_GENERIC)
	ADD_TRAIT(src, TRAIT_NO_MANIFEST_CONTENTS_ERROR, TRAIT_GENERIC)

/obj/item/storage/lockbox/order/can_unlock(mob/living/user, obj/item/card/id/id_card, silent = FALSE)
	if(id_card.registered_account == buyer_account)
		return TRUE
	if(!silent)
		balloon_alert(user, "incorrect bank account!")
	return FALSE

///screentips for lockboxes
/obj/item/storage/lockbox/add_context(atom/source, list/context, obj/item/held_item, mob/user)
	if(!held_item)
		return NONE
	if(src.broken)
		return NONE
	if(!held_item.GetID())
		return NONE
	context[SCREENTIP_CONTEXT_LMB] = atom_storage.locked ? "Unlock with ID" : "Lock with ID"
	return CONTEXTUAL_SCREENTIP_SET

/obj/item/storage/lockbox/vialbox
	name = "vial box"
	desc = "A small box that can hold up to six vials in a sealed enviroment."
	icon = 'icons/obj/storage/vial_box.dmi'
	w_class = WEIGHT_CLASS_NORMAL
	icon_state = "vialbox"
	req_access = list(ACCESS_MEDICAL)
	icon_locked = "vialbox"
	icon_closed = "vialbox"
	icon_broken = "vialbox"
	icon_open = "vialbox"
	custom_price = PAYCHECK_CREW / 2
	discountable = FALSE
	start_locked = FALSE

/obj/item/storage/lockbox/vialbox/update_icon()
	cut_overlays()
	var/slot = 1
	for(var/obj/item/reagent_containers/cup/vial/G in contents)
		var/mutable_appearance/vial = mutable_appearance(icon, "vialboxvial[slot]")
		var/mutable_appearance/filling = mutable_appearance(icon, "vialboxvial[slot]-")
		if(G.reagents.total_volume)
			var/percent = round((G.reagents.total_volume / G.volume) * 100)
			switch(percent)
				if(75 to 79)
					filling.icon_state = "vialboxvial[slot]-75"
				if(80 to 90)
					filling.icon_state = "vialboxvial[slot]-80"
				if(91 to INFINITY)
					filling.icon_state = "vialboxvial[slot]-100"

			filling.color = mix_color_from_reagents(G.reagents.reagent_list)
		add_overlay(vial)
		add_overlay(filling)
		slot++
	..()

/obj/item/storage/lockbox/vialbox/Initialize(mapload)
	. = ..()
	atom_storage.max_specific_storage = WEIGHT_CLASS_SMALL
	atom_storage.max_slots = 6
	atom_storage.max_total_storage = 12
	atom_storage.set_holdable(list(/obj/item/reagent_containers/cup/vial))
	if(!broken)
		var/mutable_appearance/led = mutable_appearance(icon, "led[atom_storage.locked]")
		add_overlay(led)
	if(!open)
		var/mutable_appearance/lid = mutable_appearance(icon, "vialboxcover")
		add_overlay(lid)
	update_icon()

/obj/item/storage/lockbox/vialbox/hypo_deluxe
	name = "deluxe hypospray vial box"
	desc = "A small box that can hold up to six vials in a sealed enviroment. This one contains a plethora of different vials for various medical ailments, designed for use in a deluxe hypospray."
	start_locked = TRUE
	req_access = list(ACCESS_CMO)

/obj/item/storage/lockbox/vialbox/hypo_deluxe/PopulateContents()
	new /obj/item/reagent_containers/cup/vial/large/bluespace/omnizine(src)
	new /obj/item/reagent_containers/cup/vial/large/bluespace/sal_acid(src)
	new /obj/item/reagent_containers/cup/vial/large/bluespace/oxandrolone(src)
	new /obj/item/reagent_containers/cup/vial/large/bluespace/pen_acid(src)
	new /obj/item/reagent_containers/cup/vial/large/bluespace/oxy(src)
	new /obj/item/reagent_containers/cup/vial/large/bluespace/atropine(src)
