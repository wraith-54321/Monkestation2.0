/**
 * # Generic restraints
 *
 * Parent class for handcuffs and handcuff accessories
 *
 * Functionality:
 * 1. A special suicide
 * 2. If a restraint is handcuffing/legcuffing a carbon while being deleted, it will remove the handcuff/legcuff status.
*/

/obj/item/restraints
	breakouttime = 1 MINUTES
	dye_color = DYE_PRISONER
	icon = 'icons/obj/restraints.dmi'

/obj/item/restraints/suicide_act(mob/living/carbon/user)
	user.visible_message(span_suicide("[user] is strangling [user.p_them()]self with [src]! It looks like [user.p_theyre()] trying to commit suicide!"))
	return OXYLOSS

/**
 * # Handcuffs
 *
 * Stuff that makes humans unable to use hands
 *
 * Clicking people with those will cause an attempt at handcuffing them to occur
*/
/obj/item/restraints/handcuffs
	name = "handcuffs"
	desc = "Use this to keep prisoners in line."
	gender = PLURAL
	icon_state = "handcuff"
	worn_icon_state = "handcuff"
	inhand_icon_state = "handcuff"
	lefthand_file = 'icons/mob/inhands/equipment/security_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/security_righthand.dmi'
	flags_1 = CONDUCT_1
	slot_flags = ITEM_SLOT_BELT | ITEM_SLOT_HANDCUFFED
	throwforce = 0
	w_class = WEIGHT_CLASS_SMALL
	throw_speed = 3
	throw_range = 5
	custom_materials = list(/datum/material/iron= SMALL_MATERIAL_AMOUNT * 5)
	/// Breakout time for the cuffs
	breakouttime = 1 MINUTES
	/// Time to hand cuff someone
	var/handcuff_time = 3 SECONDS
	///Multiplier for handcuff time
	var/handcuff_time_mod = 1
	armor_type = /datum/armor/restraints_handcuffs
	custom_price = PAYCHECK_COMMAND * 0.35
	///Sound that plays when starting to put handcuffs on someone
	var/cuffsound = 'sound/weapons/handcuffs.ogg'
	///If set, handcuffs will be destroyed on application and leave behind whatever this is set to.
	var/trashtype = null
	/// How strong the cuffs are. Weak cuffs can be broken with wirecutters or boxcutters.
	var/restraint_strength = HANDCUFFS_TYPE_STRONG

/datum/armor/restraints_handcuffs
	fire = 50
	acid = 50

/obj/item/restraints/handcuffs/attack(mob/living/carbon/C, mob/living/user)
	if(!istype(C))
		return

	if(SEND_SIGNAL(C, COMSIG_CARBON_CUFF_ATTEMPTED, user) & COMSIG_CARBON_CUFF_PREVENT)
		return

	if(iscarbon(user) && (HAS_TRAIT(user, TRAIT_CLUMSY) && prob(50))) //Clumsy people have a 50% chance to handcuff themselves instead of their target.
		to_chat(user, span_warning("Uh... how do those things work?!"))
		apply_cuffs(user,user)
		return

	if(!C.handcuffed)
		if(C.canBeHandcuffed())
			C.visible_message(span_danger("[user] is trying to put [src] on [C]!"), \
								span_userdanger("[user] is trying to put [src] on you!"))
			if(C.is_blind())
				to_chat(C, span_userdanger("As you feel someone grab your wrists, [src] start digging into your skin!"))

			playsound(loc, cuffsound, 30, TRUE, -2)
			log_combat(user, C, "attempted to handcuff", src, "Cuff Time: [DisplayTimeText(handcuff_time * handcuff_time_mod)]. Uncuff Time: [DisplayTimeText(breakouttime)].")

			if(HAS_TRAIT(user, TRAIT_FAST_CUFFING))
				handcuff_time_mod = 0.75
			else
				handcuff_time_mod = 1

			if(do_after(user, handcuff_time * handcuff_time_mod, C, timed_action_flags = IGNORE_SLOWDOWNS) && C.canBeHandcuffed())
				if(iscyborg(user))
					apply_cuffs(C, user, TRUE)
				else
					apply_cuffs(C, user)
				C.visible_message(span_notice("[user] handcuffs [C]."), \
									span_userdanger("[user] handcuffs you."))
				SSblackbox.record_feedback("tally", "handcuffs", 1, type)

				log_combat(user, C, "handcuffed", src, "Cuff Time: [DisplayTimeText(handcuff_time * handcuff_time_mod)]. Uncuff Time: [DisplayTimeText(breakouttime)].")
			else
				to_chat(user, span_warning("You fail to handcuff [C]!"))
				log_combat(user, C, "failed to handcuff", src)
		else
			to_chat(user, span_warning("[C] doesn't have two hands..."))

/**
 * This handles handcuffing people
 *
 * When called, this instantly puts handcuffs on someone (if possible)
 * Arguments:
 * * mob/living/carbon/target - Who is being handcuffed
 * * mob/user - Who or what is doing the handcuffing
 * * dispense - True if the cuffing should create a new item instead of using putting src on the mob, false otherwise. False by default.
*/
/obj/item/restraints/handcuffs/proc/apply_cuffs(mob/living/carbon/target, mob/user, dispense = FALSE)
	if(target.handcuffed)
		return

	if(!user.temporarilyRemoveItemFromInventory(src) && !dispense)
		return

	var/obj/item/restraints/handcuffs/cuffs = src
	if(trashtype)
		cuffs = new trashtype()
	else if(dispense)
		cuffs = new type()

	target.equip_to_slot(cuffs, ITEM_SLOT_HANDCUFFED)

	if(trashtype && !dispense)
		qdel(src)
	return

/obj/item/restraints/handcuffs/silver
	name = "silver handcuffs"
	desc = "A pair of silver handcuffs. Their brittle construction allows them to be used only once, and normal crew have little trouble breaking out of them, even while being moved. \
	But some say they can contain certain creatures of the night..."
	breakouttime = 45 SECONDS

	trashtype = /obj/item/restraints/handcuffs/silver/used

	color = list(
		1, 0, 0,
		0, 1, 0,
		0, 0, 1,
		0.4,0.4,0.4
	)

/obj/item/restraints/handcuffs/silver/used
	item_flags = DROPDEL

/obj/item/restraints/handcuffs/silver/used/equipped(mob/user, slot, initial)
	. = ..()
	if(!IS_BLOODSUCKER_OR_VASSAL(user))
		breakout_while_moving = TRUE

/obj/item/restraints/handcuffs/silver/used/dropped(mob/user)
	user.visible_message(span_danger("\The [src] shatter into a hundred pieces!"))

	return ..()

/obj/item/restraints/handcuffs/silver/apply_cuffs(mob/living/carbon/target, mob/user, dispense = FALSE)
	. = ..()

	if (target.handcuffed && IS_BLOODSUCKER_OR_VASSAL(target))
		target.apply_status_effect(/datum/status_effect/silver_cuffed)

/**
 * # Alien handcuffs
 *
 * Abductor reskin of the handcuffs.
*/
/obj/item/restraints/handcuffs/alien
	icon_state = "handcuffAlien"

/**
 *
 * # Fake handcuffs
 *
 * Fake handcuffs that can be removed near-instantly.
*/
/obj/item/restraints/handcuffs/fake
	name = "fake handcuffs"
	desc = "Fake handcuffs meant for gag purposes."
	breakouttime = 1 SECONDS
	restraint_strength = HANDCUFFS_TYPE_WEAK

/**
 * # Cable restraints
 *
 * Ghetto handcuffs. Removing those is faster.
*/
/obj/item/restraints/handcuffs/cable
	name = "cable restraints"
	desc = "Looks like some cables tied together. Could be used to tie something up."
	icon_state = "cuff"
	inhand_icon_state = "coil_red"
	color = CABLE_HEX_COLOR_RED
	///for generating the correct icons based off the original cable's color.
	var/cable_color = CABLE_COLOR_RED
	lefthand_file = 'icons/mob/inhands/equipment/tools_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/tools_righthand.dmi'
	custom_materials = list(/datum/material/iron= SMALL_MATERIAL_AMOUNT * 1.5, /datum/material/glass= SMALL_MATERIAL_AMOUNT * 0.75)
	breakouttime = 30 SECONDS
	cuffsound = 'sound/weapons/cablecuff.ogg'
	restraint_strength = HANDCUFFS_TYPE_WEAK

/obj/item/restraints/handcuffs/cable/Initialize(mapload, new_color)
	. = ..()

	var/static/list/hovering_item_typechecks = list(
		/obj/item/stack/rods = list(
			SCREENTIP_CONTEXT_LMB = "Craft wired rod",
		),

		/obj/item/stack/sheet/iron = list(
			SCREENTIP_CONTEXT_LMB = "Craft bola",
		),
	)

	AddElement(/datum/element/contextual_screentip_item_typechecks, hovering_item_typechecks)
	AddElement(/datum/element/update_icon_updates_onmob, (slot_flags|ITEM_SLOT_HANDCUFFED))

	if(new_color)
		set_cable_color(new_color)

/obj/item/restraints/handcuffs/cable/proc/set_cable_color(new_color)
	color = GLOB.cable_colors[new_color]
	cable_color = new_color
	update_appearance(UPDATE_ICON)

/obj/item/restraints/handcuffs/cable/vv_edit_var(vname, vval)
	if(vname == NAMEOF(src, cable_color))
		set_cable_color(vval)
		datum_flags |= DF_VAR_EDITED
		return TRUE
	return ..()

/obj/item/restraints/handcuffs/cable/update_icon_state()
	. = ..()
	if(cable_color)
		var/new_inhand_icon = "coil_[cable_color]"
		if(new_inhand_icon != inhand_icon_state)
			inhand_icon_state = new_inhand_icon //small memory optimization.

/**
 * # Sinew restraints
 *
 * Primal ghetto handcuffs
 *
 * Just cable restraints that look differently and can't be recycled.
*/
/obj/item/restraints/handcuffs/cable/sinew
	name = "sinew restraints"
	desc = "A pair of restraints fashioned from long strands of flesh."
	icon_state = "sinewcuff"
	inhand_icon_state = null
	cable_color = null
	custom_materials = null
	color = null

/**
 * Red cable restraints
*/
/obj/item/restraints/handcuffs/cable/red
	color = CABLE_HEX_COLOR_RED
	cable_color = CABLE_COLOR_RED
	inhand_icon_state = "coil_red"

/**
 * Yellow cable restraints
*/
/obj/item/restraints/handcuffs/cable/yellow
	color = CABLE_HEX_COLOR_YELLOW
	cable_color = CABLE_COLOR_YELLOW
	inhand_icon_state = "coil_yellow"

/**
 * Blue cable restraints
*/
/obj/item/restraints/handcuffs/cable/blue
	color =CABLE_HEX_COLOR_BLUE
	cable_color = CABLE_COLOR_BLUE
	inhand_icon_state = "coil_blue"

/**
 * Green cable restraints
*/
/obj/item/restraints/handcuffs/cable/green
	color = CABLE_HEX_COLOR_GREEN
	cable_color = CABLE_COLOR_GREEN
	inhand_icon_state = "coil_green"

/**
 * Pink cable restraints
*/
/obj/item/restraints/handcuffs/cable/pink
	color = CABLE_HEX_COLOR_PINK
	cable_color = CABLE_COLOR_PINK
	inhand_icon_state = "coil_pink"

/**
 * Orange (the color) cable restraints
*/
/obj/item/restraints/handcuffs/cable/orange
	color = CABLE_HEX_COLOR_ORANGE
	cable_color = CABLE_COLOR_ORANGE
	inhand_icon_state = "coil_orange"

/**
 * Cyan cable restraints
*/
/obj/item/restraints/handcuffs/cable/cyan
	color = CABLE_HEX_COLOR_CYAN
	cable_color = CABLE_COLOR_CYAN
	inhand_icon_state = "coil_cyan"

/**
 * White cable restraints
*/
/obj/item/restraints/handcuffs/cable/white
	color = CABLE_HEX_COLOR_WHITE
	cable_color = CABLE_COLOR_WHITE
	inhand_icon_state = "coil_white"

/obj/item/restraints/handcuffs/cable/attackby(obj/item/attacking_item, mob/user, list/modifiers, list/attack_modifiers) //Slapcrafting
	if(istype(attacking_item, /obj/item/stack/rods))
		var/obj/item/stack/rods/R = attacking_item
		if (R.use(1))
			var/obj/item/wirerod/W = new /obj/item/wirerod
			remove_item_from_storage(user)
			user.put_in_hands(W)
			to_chat(user, span_notice("You wrap [src] around the top of [attacking_item]."))
			qdel(src)
		else
			to_chat(user, span_warning("You need one rod to make a wired rod!"))
			return
	else if(istype(attacking_item, /obj/item/stack/sheet/iron))
		var/obj/item/stack/sheet/iron/M = attacking_item
		if(M.get_amount() < 6)
			to_chat(user, span_warning("You need at least six iron sheets to make good enough weights!"))
			return
		to_chat(user, span_notice("You begin to apply [attacking_item] to [src]..."))
		if(do_after(user, 3.5 SECONDS, target = src))
			if(M.get_amount() < 6 || !M)
				return
			var/obj/item/restraints/legcuffs/bola/S = new /obj/item/restraints/legcuffs/bola
			M.use(6)
			user.put_in_hands(S)
			to_chat(user, span_notice("You make some weights out of [attacking_item] and tie them to [src]."))
			remove_item_from_storage(user)
			qdel(src)
	else
		return ..()

/**
 * # Zipties
 *
 * One-use handcuffs that take 45 seconds to resist out of instead of one minute. This turns into the used version when applied.
*/
/obj/item/restraints/handcuffs/cable/zipties
	name = "zipties"
	desc = "Plastic, disposable zipties that can be used to restrain temporarily but are destroyed after use."
	icon_state = "cuff"
	inhand_icon_state = "cuff_white"
	lefthand_file = 'icons/mob/inhands/equipment/security_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/security_righthand.dmi'
	custom_materials = null
	breakouttime = 45 SECONDS
	trashtype = /obj/item/restraints/handcuffs/cable/zipties/used
	color = null
	cable_color = null

/**
 * # Used zipties
 *
 * What zipties turn into when applied. These can't be used to cuff people.
*/
/obj/item/restraints/handcuffs/cable/zipties/used
	desc = "A pair of broken zipties."
	icon_state = "cuff_used"

/obj/item/restraints/handcuffs/cable/zipties/used/attack()
	return

/**
 * # Fake Zipties
 *
 * One-use handcuffs that is very easy to break out of, meant as a one-use alternative to regular fake handcuffs.
 */
/obj/item/restraints/handcuffs/cable/zipties/fake
	name = "fake zipties"
	desc = "Fake zipties meant for gag purposes."
	breakouttime = 1 SECONDS

/obj/item/restraints/handcuffs/cable/zipties/fake/used
	desc = "A pair of broken fake zipties."
	icon_state = "cuff_used"

/**
 * Handcuffs used for the security holobarrier projector
 * the handcuffs themselfes should be un-obtainable, /used version is applied on our actual target
 * as strong zipties, take 50% longer to handcuff someone with
 */

/obj/item/restraints/handcuffs/holographic
	name = "holographic energy field"
	desc = "A weirdly solid holographic field... how did you get this? this item gives you the permission to scream at coders."
	icon_state = "handcuffAlien"
	lefthand_file = 'icons/mob/inhands/equipment/security_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/security_righthand.dmi'
	breakouttime = 45 SECONDS
	trashtype = /obj/item/restraints/handcuffs/holographic/used
	flags_1 = NONE

/obj/item/restraints/handcuffs/holographic/used
	desc = "A holographic projection of handcuffs, suprisingly hard to break out of"
	item_flags = DROPDEL

/obj/item/restraints/handcuffs/holographic/used/dropped(mob/user)
	user.visible_message(span_danger("[user]'s [name] dissapears!"), \
							span_userdanger("[user]'s [name] dissapears!"))
	. = ..()

/**
 * # Generic leg cuffs
 *
 * Parent class for everything that can legcuff carbons. Can't legcuff anything itself.
*/
/obj/item/restraints/legcuffs
	name = "leg cuffs"
	desc = "Use this to keep prisoners in line."
	gender = PLURAL
	icon_state = "handcuff"
	inhand_icon_state = "handcuff"
	lefthand_file = 'icons/mob/inhands/equipment/security_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/security_righthand.dmi'
	flags_1 = CONDUCT_1
	throwforce = 0
	w_class = WEIGHT_CLASS_NORMAL
	slowdown = 7
	breakouttime = 30 SECONDS
	slot_flags = ITEM_SLOT_LEGCUFFED
	item_flags = IMMUTABLE_SLOW

/**
 * # Bear trap
 *
 * This opens, closes, and bites people's legs.
 */
/obj/item/restraints/legcuffs/beartrap
	name = "bear trap"
	throw_speed = 1
	throw_range = 1
	drag_slowdown = 1
	icon_state = "beartrap"
	desc = "A trap used to catch bears and other legged creatures."
	gender = NEUTER
	///If true, the trap is "open" and can trigger.
	var/armed = FALSE
	///How much damage the trap deals when triggered.
	var/trap_damage = 20

/obj/item/restraints/legcuffs/beartrap/prearmed
	armed = TRUE

/obj/item/restraints/legcuffs/beartrap/Initialize(mapload)
	. = ..()
	update_appearance()
	var/static/list/loc_connections = list(
		COMSIG_ATOM_ENTERED = PROC_REF(spring_trap),
	)
	AddElement(/datum/element/connect_loc, loc_connections)

/obj/item/restraints/legcuffs/beartrap/update_icon_state()
	icon_state = "[initial(icon_state)][armed]"
	return ..()

/obj/item/restraints/legcuffs/beartrap/suicide_act(mob/living/user)
	user.visible_message(span_suicide("[user] is sticking [user.p_their()] head in the [src.name]! It looks like [user.p_theyre()] trying to commit suicide!"))
	playsound(loc, 'sound/weapons/bladeslice.ogg', 50, TRUE, -1)
	return BRUTELOSS

/obj/item/restraints/legcuffs/beartrap/proc/set_arm(toggle_state, mob/user, silent = TRUE)
	armed = toggle_state
	if(armed)
		w_class = WEIGHT_CLASS_BULKY
		if(!silent)
			playsound(src, 'sound/weapons/handcuffs.ogg', 30, FALSE, -3)
	else
		w_class = WEIGHT_CLASS_NORMAL
		if(!silent)
			playsound(src, 'sound/weapons/handcuffs.ogg', 40, FALSE, -5)

	update_appearance(UPDATE_ICON)


/obj/item/restraints/legcuffs/beartrap/attack_self(mob/living/user)
	. = ..()
	if(!ishuman(user) || user.stat != CONSCIOUS || HAS_TRAIT(user, TRAIT_HANDS_BLOCKED))
		return

	if(armed && (HAS_TRAIT(user, TRAIT_DUMB) || HAS_TRAIT(user, TRAIT_CLUMSY)) && prob(25))
		to_chat(user, span_warning("Your hand slips, setting off the trigger!"))
		var/hand_zone = user.held_index_to_dir(user.active_hand_index) == "r" ? BODY_ZONE_PRECISE_R_HAND : BODY_ZONE_PRECISE_L_HAND
		spring_trap(user, def_zone = hand_zone)
		return

	var/is_expert = user.mind?.get_skill_level(/datum/skill/cleaning) >=  SKILL_LEVEL_MASTER
	if(!is_expert && !do_after(user, 3 SECONDS, src))
		user.balloon_alert(user, "interrupted!")
		return

	set_arm(!armed, user, FALSE)

	user.visible_message(span_notice("[user][is_expert ? " expertly " : " "][armed ? "arms" : "disarms"] \the [src]!"), span_notice("\The [src] is now [armed ? "armed" : "disarmed"]!"))

/obj/item/restraints/legcuffs/beartrap/attempt_pickup(mob/user)
	if(!armed)
		return ..()

	if(!ishuman(user) || user.stat != CONSCIOUS || HAS_TRAIT(user, TRAIT_HANDS_BLOCKED))
		return

	if(armed && (HAS_TRAIT(user, TRAIT_DUMB) || HAS_TRAIT(user, TRAIT_CLUMSY)) && prob(25))
		to_chat(user, span_warning("Your hand slips, setting off the trigger!"))
		var/hand_zone = user.held_index_to_dir(user.active_hand_index) == "r" ? BODY_ZONE_PRECISE_R_HAND : BODY_ZONE_PRECISE_L_HAND
		spring_trap(user, def_zone = hand_zone)
		return

	var/is_expert = user.mind?.get_skill_level(/datum/skill/cleaning) >=  SKILL_LEVEL_MASTER
	if(!is_expert && !do_after(user, 3 SECONDS, src))
		user.balloon_alert(user, "interrupted!")
		return

	set_arm(!armed, user, FALSE)

	user.visible_message(span_notice("[user][is_expert ? " expertly " : " "]disarms \the [src]!"), span_notice("\The [src] is now disarmed!"))

	set_arm(FALSE, user, FALSE)

	return ..()

/// Extra checks for if the trap should close on the victim. Used by subtypes mostly.
/obj/item/restraints/legcuffs/beartrap/proc/is_valid_salad(mob/living/carbon/victim)
	return TRUE

/**
 * Closes a bear trap
 *
 * Closes a bear trap.
 * Arguments:
 */
/obj/item/restraints/legcuffs/beartrap/proc/close_trap()
	set_arm(FALSE, FALSE)
	playsound(src, 'sound/effects/snap.ogg', 50, TRUE)

/obj/item/restraints/legcuffs/beartrap/proc/spring_trap(datum/source, atom/movable/target, thrown_at = FALSE, def_zone = BODY_ZONE_CHEST)
	SIGNAL_HANDLER
	if(!armed)
		return

	if(isitem(target))
		var/obj/item/bait = target
		if(bait.w_class >= WEIGHT_CLASS_SMALL)
			close_trap()
			target.visible_message(span_danger("\The [bait] triggers \the [src]!"))
			return

	if(isprojectile(target))
		var/obj/projectile/bait_projectile = target
		if(bait_projectile.original == src && bait_projectile.damage >= 5)
			close_trap()
			target.visible_message(span_danger("\The [bait_projectile] triggers \the [src]!"))
			return

	if(!isliving(target))
		return

	var/mob/living/victim = target
	if(!is_valid_salad(victim))
		return
	if(istype(victim.buckled, /obj/vehicle))
		var/obj/vehicle/ridden_vehicle = victim.buckled
		if(!ridden_vehicle.are_legs_exposed) //close the trap without injuring/trapping the rider if their legs are inside the vehicle at all times.
			close_trap()
			ridden_vehicle.visible_message(span_danger("[ridden_vehicle] triggers \the [src]!"))
			return

	//don't close the trap if they're as small as a mouse, or not touching the ground
	if(victim.mob_size <= MOB_SIZE_TINY || (!thrown_at && victim.movement_type & (FLYING|FLOATING)))
		return

	close_trap()
	if(thrown_at)
		victim.visible_message(span_danger("\The [src] ensnares [victim]!"), \
				span_userdanger("\The [src] ensnares you!"))
	else
		victim.visible_message(span_danger("[victim] triggers \the [src]."), \
				span_userdanger("You trigger \the [src]!"))

	if(iscarbon(victim) && victim.body_position == STANDING_UP && !((def_zone == BODY_ZONE_PRECISE_R_HAND) || (def_zone == BODY_ZONE_PRECISE_L_HAND)))
		var/mob/living/carbon/carbon_victim = victim
		def_zone = pick(BODY_ZONE_L_LEG, BODY_ZONE_R_LEG)
		if(!carbon_victim.legcuffed && carbon_victim.num_legs >= 2) //beartrap can't cuff your leg if there's already a beartrap or legcuffs, or you don't have two legs.
			INVOKE_ASYNC(carbon_victim, TYPE_PROC_REF(/mob/living/carbon, equip_to_slot), src, ITEM_SLOT_LEGCUFFED)
			SSblackbox.record_feedback("tally", "handcuffs", 1, type)

	victim.apply_damage(trap_damage, BRUTE, def_zone, sharpness = SHARP_POINTY, wound_bonus=trap_damage/2, bare_wound_bonus = trap_damage, blocked = victim.run_armor_check(def_zone, MELEE))
/**
 * # Energy snare
 *
 * This closes on people's legs.
 *
 * A weaker version of the bear trap that can be resisted out of faster and disappears
 */
/obj/item/restraints/legcuffs/beartrap/energy
	name = "energy snare"
	armed = 1
	icon_state = "e_snare"
	trap_damage = 0
	breakouttime = 3 SECONDS
	item_flags = DROPDEL | IMMUTABLE_SLOW
	flags_1 = NONE

/obj/item/restraints/legcuffs/beartrap/energy/Initialize(mapload)
	. = ..()
	addtimer(CALLBACK(src, PROC_REF(dissipate)), 15 SECONDS)

/**
 * Handles energy snares disappearing
 *
 * If the snare isn't closed on anyone, it will disappear in a shower of sparks.
 * Arguments:
 */
/obj/item/restraints/legcuffs/beartrap/energy/proc/dissipate()
	if(!ismob(loc))
		do_sparks(1, TRUE, src)
		qdel(src)

/obj/item/restraints/legcuffs/beartrap/energy/emp_act(severity)
	do_sparks(rand(1, 3), FALSE, src)
	visible_message(span_warning("\The [src] overloads!"))
	if(!isturf(loc))
		do_sparks(1, TRUE, src)
		qdel(src)
		return
	close_trap()

/obj/item/restraints/legcuffs/beartrap/energy/attack_hand(mob/user, list/modifiers)
	dissipate()

/obj/item/restraints/legcuffs/beartrap/energy/cyborg
	breakouttime = 2 SECONDS // Cyborgs shouldn't have a strong restraint

/**
 * # Security beartrap
 *
 * This closes on people's legs only if they are wanted or incarcerated.
 *
 * If it's emagged it only closes on security.
 */
/obj/item/restraints/legcuffs/beartrap/security
	name = "security trap"
	icon_state = "sectrap"
	desc = "A rubber padded trap used to catch criminals non-lethally. Relies on security record data to function."
	trap_damage = 0
	breakouttime = 5 SECONDS
	custom_price = PAYCHECK_COMMAND

/obj/item/restraints/legcuffs/beartrap/security/Initialize(mapload)
	. = ..()
	update_appearance(UPDATE_OVERLAYS)

/obj/item/restraints/legcuffs/beartrap/security/update_overlays()
	. = ..()
	if(obj_flags & EMAGGED)
		. += "sectrap_emag"
		return
	. = "sectrap_[armed ? "on" : "off"]"

/obj/item/restraints/legcuffs/beartrap/security/emp_act(severity)
	do_sparks(rand(1,3), FALSE, src)
	if(prob(50))
		close_trap()
	else
		emag_act()
	visible_message(span_warning("\The [src] overloads!"))

/obj/item/restraints/legcuffs/beartrap/security/emag_act(mob/user)
	if(obj_flags & EMAGGED)
		return
	obj_flags |= EMAGGED
	update_appearance(UPDATE_ICON)
	trap_damage = 30
	do_sparks(3, FALSE, src)
	sleep(1 SECOND)
	playsound(src, 'sound/machines/microwave/microwave-end.ogg', 100, FALSE)
	if(user)
		balloon_alert(user, "biometric scanner set!")

/obj/item/restraints/legcuffs/beartrap/security/is_valid_salad(mob/living/carbon/victim)
	if(obj_flags & EMAGGED)
		var/obj/item/organ/internal/liver/liver = victim.get_organ_slot(ORGAN_SLOT_LIVER)
		if(liver && HAS_TRAIT(liver, TRAIT_LAW_ENFORCEMENT_METABOLISM))
			return TRUE
		else
			return FALSE
	var/obj/item/card/id/idcard = victim.get_idcard(FALSE)
	if(istype(idcard, /obj/item/card/id/advanced/chameleon))
		return FALSE
	var/perpname = victim.get_face_name(victim.get_id_name())
	var/datum/record/crew/record = find_record(perpname)
	if(!record)
		return FALSE
	if((record.wanted_status == WANTED_ARREST || record.wanted_status == WANTED_PRISONER))
		return TRUE
	return FALSE

/obj/item/restraints/legcuffs/beartrap/slasher
	name = "barbed bear trap"
	alpha = 160
	var/datum/antagonist/slasher/slasher_owner

/obj/item/restraints/legcuffs/beartrap/slasher/Destroy()
	if(slasher_owner)
		slasher_owner.linked_traps -= src
	return ..()

/obj/item/restraints/legcuffs/beartrap/slasher/proc/set_slasher(datum/antagonist/slasher/slasherdatum)
	if(slasher_owner)
		slasher_owner.linked_traps -= src
	slasher_owner = slasherdatum
	if(slasher_owner)
		slasher_owner.linked_traps += src

/obj/item/restraints/legcuffs/bola
	name = "bola"
	desc = "A restraining device designed to be thrown at the target. Upon connecting with said target, it will wrap around their legs, making it difficult for them to move quickly."
	icon_state = "bola"
	icon_state_preview = "bola_preview"
	inhand_icon_state = "bola"
	lefthand_file = 'icons/mob/inhands/weapons/thrown_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/weapons/thrown_righthand.dmi'
	breakouttime = 3.5 SECONDS//easy to apply, easy to break out of
	gender = NEUTER
	///Amount of time to knock the target down for once it's hit in deciseconds.
	var/knockdown = 0

/obj/item/restraints/legcuffs/bola/throw_at(atom/target, range, speed, mob/thrower, spin=1, diagonals_first = 0, datum/callback/callback, gentle = FALSE, quickstart = TRUE)
	if(!..())
		return
	playsound(src.loc,'sound/weapons/bolathrow.ogg', 75, TRUE)

/obj/item/restraints/legcuffs/bola/throw_impact(atom/hit_atom, datum/thrownthing/throwingdatum)
	if(..() || !iscarbon(hit_atom))//if it gets caught or the target can't be cuffed,
		return//abort
	ensnare(hit_atom)

/**
 * Attempts to legcuff someone with the bola
 *
 * Arguments:
 * * C - the carbon that we will try to ensnare
 */
/obj/item/restraints/legcuffs/bola/proc/ensnare(mob/living/carbon/C)
	if(!C.legcuffed && C.num_legs >= 2)
		visible_message(span_danger("\The [src] ensnares [C]!"), span_userdanger("\The [src] ensnares you!"))
		C.equip_to_slot(src, ITEM_SLOT_LEGCUFFED)
		SSblackbox.record_feedback("tally", "handcuffs", 1, type)
		C.Knockdown(knockdown)
		playsound(src, 'sound/effects/snap.ogg', 50, TRUE)

/**
 * A traitor variant of the bola.
 *
 * It knocks people down and is harder to remove.
 */
/obj/item/restraints/legcuffs/bola/tactical
	name = "reinforced bola"
	desc = "A strong bola, made with a long steel chain. It looks heavy, enough so that it could trip somebody."
	icon_state = "bola_r"
	inhand_icon_state = "bola_r"
	breakouttime = 7 SECONDS
	knockdown = 3.5 SECONDS

/**
 * A security variant of the bola.
 *
 * It's smaller and uncatchable (although still blockable), but easier to remove than a normal bola, slows slightly less, and non-reusable.
 */
/obj/item/restraints/legcuffs/bola/energy
	name = "energy bola"
	desc = "A specialized hard-light bola designed to ensnare fleeing criminals and aid in arrests."
	icon_state = "ebola"
	inhand_icon_state = "ebola"
	hitsound = 'sound/weapons/taserhit.ogg'
	w_class = WEIGHT_CLASS_SMALL
	breakouttime = 2 SECONDS
	slowdown = 5
	custom_price = PAYCHECK_COMMAND * 0.35

/obj/item/restraints/legcuffs/bola/energy/Initialize(mapload)
	. = ..()
	ADD_TRAIT(src, TRAIT_UNCATCHABLE, TRAIT_GENERIC) // People said energy bolas being uncatchable is a feature.

/obj/item/restraints/legcuffs/bola/energy/ensnare(mob/living/carbon/C)
	. = ..()

	if (C.legcuffed == src)
		src.item_flags |= DROPDEL
/**
 * A pacifying variant of the bola.
 *
 * It's much harder to remove, doesn't cause a slowdown and gives people /datum/status_effect/gonbola_pacify.
 */
/obj/item/restraints/legcuffs/bola/gonbola
	name = "gonbola"
	desc = "Hey, if you have to be hugged in the legs by anything, it might as well be this little guy."
	icon_state = "gonbola"
	icon_state_preview = "gonbola_preview"
	inhand_icon_state = "bola_r"
	breakouttime = 30 SECONDS
	slowdown = 0
	var/datum/status_effect/gonbola_pacify/effectReference

/obj/item/restraints/legcuffs/bola/gonbola/throw_impact(atom/hit_atom, datum/thrownthing/throwingdatum)
	. = ..()
	if(iscarbon(hit_atom))
		var/mob/living/carbon/C = hit_atom
		effectReference = C.apply_status_effect(/datum/status_effect/gonbola_pacify)

/obj/item/restraints/legcuffs/bola/gonbola/dropped(mob/user)
	. = ..()
	if(effectReference)
		QDEL_NULL(effectReference)
